repositories {
    flatDir {
        dirs 'libs'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'dbtools'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "net.pside.android.example.mostpowerfulorminandroid"
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 2
        versionName "2015.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        ormlite
        greendao
        activeandroid

        ollie
        sugarorm
        dbflow
        dbTools
        shillelagh
        torch
        cupboard

        slimrepo
        rushorm
        squidb
        orma
        easyliteorm
        triorm
        quantumflux
        cporm

        realm
        snappydb
        mapdb
        paper
        sabres
    }
    sourceSets {
        greendao {
            java.srcDirs = ['src/greendao/java-gen']
        }
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/README'
        exclude 'META-INF/NOTICE'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}
dependencies {
//    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:support-v4:23.1.1'

    ///// 古参ORM /////
    // ORMLite
    ormliteCompile 'com.j256.ormlite:ormlite-android:4.48'

    // greenDAO
    greendaoCompile 'de.greenrobot:greendao:2.1.0'

    // ActiveAndroid
    activeandroidCompile(name: 'activeandroid-3.0', ext: 'jar')

    ///// 中堅クラス /////
    // Ollie
    ollieProvided 'com.michaelpardo:ollie-compiler:0.3.1'
    ollieCompile 'com.michaelpardo:ollie:0.3.1'

    // SugarORM
    sugarormCompile 'com.github.satyan:sugar:1.4'

    // DBFlow
    apt 'com.raizlabs.android:DBFlow-Compiler:2.2.1'
    dbflowCompile 'com.raizlabs.android:DBFlow-Core:2.2.1'
    dbflowCompile 'com.raizlabs.android:DBFlow:2.2.1'

    // DBTools
    dbToolsCompile 'org.dbtools:dbtools-android:4.1.2'
    dbToolsCompile 'org.dbtools:dbtools-query:2.5.6' // optional

    // Shillelagh
    shillelaghCompile 'com.andrewreitz:shillelagh:0.5.0'
    shillelaghProvided 'com.andrewreitz:shillelagh-processor:0.5.0'

    // Torch
    apt "org.brightify.torch:torch-compiler:1.0.0-alpha.3"
    torchCompile "org.brightify.torch:torch-android:1.0.0-alpha.3"

    // Cupboard
    cupboardCompile 'nl.qbusict:cupboard:2.1.4'

    ///// 期待の新星 /////
    // Slim Repo
    apt 'com.slimgears.slimrepo:slimrepo-apt:0.9.0'
    slimrepoCompile 'com.slimgears.slimrepo:slimrepo-android:0.9.0'

    // RushOrm
    rushormCompile 'co.uk.rushorm:rushandroid:1.1.11'

    // SquiDB
    apt 'com.yahoo.squidb:squidb-processor:2.0.1'
    squidbCompile 'com.yahoo.squidb:squidb:2.0.1'
    squidbCompile 'com.yahoo.squidb:squidb-annotations:2.0.1'

    // Orma
    apt 'com.github.gfx.android.orma:orma-processor:0.0.1'
    ormaProvided 'com.github.gfx.android.orma:orma-annotations:0.0.1'
    ormaCompile 'com.github.gfx.android.orma:orma:0.0.1'

    // EasyliteORM
    compile 'com.easyliteorm:easyliteorm:1.2.0'

    // TriOrm
    triormCompile(name: 'triorm-20151118', ext: 'aar')

    // QuantumFlux
    quantumfluxCompile 'info.quantumflux:library:0.9.1'

    // CPOrm
    cpormCompile 'za.co.cporm:CPOrm:2.98'

    ///// NoSQL枠 /////
    // Realm
    realmCompile 'io.realm:realm-android:0.84.2'

    // SnappyDB
    snappydbCompile 'com.snappydb:snappydb-lib:0.5.2'
    snappydbCompile 'com.esotericsoftware.kryo:kryo:2.24.0'

    // MapDB
    mapdbCompile 'org.mapdb:mapdb:1.0.8'

    // Paper
    paperCompile 'io.paperdb:paperdb:1.0'

    // Sabres
    sabresCompile('com.sabres:sabres:0.9.18@aar') {
        transitive = true;
    }
}

dbtools {
    type 'ANDROID'

    basePackageName android.defaultConfig.applicationId
    outputSrcDir 'src/main/java/net/pside/android/example/mostpowerfulorminandroid'

    injectionSupport true // support for @Inject (using JEE, Dagger, Guice, etc)
    jsr305Support true // support for @Notnull / @Nullable etc
    includeDatabaseNameInPackage true
    // place each set of domain objects into a package named after its database
    eventBusSupport true // support Event Bus
    dateTimeSupport true // support Joda DateTime
    rxJavaSupport true // support RxJava
}
